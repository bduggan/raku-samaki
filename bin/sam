#!/usr/bin/env raku

use Samaki;
use Samaki::Share;
use Samaki::Page;
use Samaki::Plugins;
use Log::Async;
use Terminal::ANSI::OO;
use JSON::Fast;

logger.untapped-ok = True;
logger.send-to("samaki.log") if %*ENV<SAMAKI_DEBUG>;

my $editor = $*ENV<EDITOR> //  "vim";

sub edit(IO::Path $path) {
  put t.text-reset;
  shell <<$editor $path>>;
  exit note "no page" unless $path.IO.e;
}

#| Browse pages
multi MAIN(:$dir = $*CWD) {
  my $wkdir = $dir;
  my $q = Samaki.new(:wkdir($wkdir.IO), :$editor);
  $q.start-ui: 'browse';
}

#| Import a Jupyter notebook
multi MAIN('from-jupyter', Str $nb-filename) {
  exit note "notebook should end in .ipynb" unless $nb-filename.ends-with('ipynb');
  my $in = from-json slurp $nb-filename;
  my @out = $in<cells>.map: *<source>;
  my @lines;
  for @out {
    @lines.push: "-- repl";
    @lines.push: .join("\n");
    @lines.push: "\n";
  }
  my $samaki-name = $nb-filename.subst('.ipynb','.samaki');
  @lines.join("\n") ==> spurt $samaki-name;
  MAIN($samaki-name);
}

#| Make a new page and edit
multi MAIN('new', :$wkdir = $*CWD) {
  my $q = Samaki.new(:$wkdir, :$editor);
  my $name = prompt "Please enter a name for the new Samaki page: ";
  my $page = "$name.samaki".IO;
  edit $page;
  $q.start-ui: :page($name);
}

#| Edit an existing page
multi MAIN($target, :$wkdir = $*CWD) {
  if $target.contains('/') {
    my $page = $target.IO.basename;
    my $wkdir = $target.IO.dirname.IO;
    my $q = Samaki.new(:$wkdir, :$editor);
    $q.start-ui: :$page;
    exit;
  }
  my $q = Samaki.new(:$wkdir, :$editor);
  my $page = $target;
  $page .= subst('.samaki','') if $page.ends-with('samaki');
  $q.start-ui: :$page;
}

#| Reset the configuration to the default
multi MAIN('reset-conf') {
  my $wkdir = $*TMPDIR;
  my $q = Samaki.new(:$wkdir, :$editor);
  my $file = $q.config-file;
  if $file.IO.e {
    $file.IO.unlink;
    note "Deleted config file " ~ $file;
  } else {
    note "No config file to delete at " ~ $file;
  }
}

#| Edit the configuration file
multi MAIN('conf') {
  my $wkdir = $*TMPDIR;
  my $q = Samaki.new(:$wkdir, :$editor);
  my $path = $q.config-file;
  $path.IO.e or exit note "No config file at $path";
  shell <<$editor $path>>;
  note "Done editing config file at $path";
}

#| Generate an HTML version of a samaki file for easy sharing
multi MAIN('share', $target, :$wkdir = $*CWD, :$output) {
  my $page-path = $target.IO;
  unless $page-path.e {
    exit note "File not found: $target";
  }

  # Determine page name and working directory
  my $page-name;
  my $actual-wkdir;
  if $target.contains('/') {
    $page-name = $page-path.basename.subst(/'.samaki'$/, '');
    $actual-wkdir = $page-path.dirname.IO;
  } else {
    $page-name = $target.subst(/'.samaki'$/, '');
    $actual-wkdir = $wkdir.IO;
  }

  # Initialize Samaki to get plugins configured
  my $q = Samaki.new(:wkdir($actual-wkdir), :$editor);

  # Load the page
  my $page = Samaki::Page.new(name => $page-name, wkdir => $actual-wkdir);
  my $loaded = $page.load(plugins => $q.plugins);

  unless $loaded {
    exit note "Failed to load page: " ~ ($page.errors // "unknown error");
  }

  # Generate HTML
  my $share = Samaki::Share.new(:$page, plugins => $q.plugins);
  my $html = $share.generate-html();

  # Determine output path
  my $output-path = $output ?? $output.IO !! $actual-wkdir.child("$page-name.html");

  # Write HTML to file
  spurt $output-path, $html;

  note "âœ“ Generated HTML: $output-path";

  # Open in browser
  my $open-cmd = $*DISTRO.is-win ?? 'start' !! ($*KERNEL.name eq 'darwin' ?? 'open' !! 'xdg-open');
  shell "$open-cmd $output-path";
}
