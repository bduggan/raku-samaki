use Test;
use Samaki::Plugout::CSVGeo;

my $*SAMAKI-NO-SHELL-OPEN = True;

my $plugout = Samaki::Plugout::CSVGeo.new;

ok $plugout, 'CSVGeo plugout created';
is $plugout.name, 'csv-geo', 'plugout has correct name';

# Test lat/lon detection
my @latlon-cols = <name lat lon>;
my @pairs = $plugout.detect-latlon-pairs(@latlon-cols);
is @pairs.elems, 1, 'detected one lat/lon pair';
is @pairs[0]<lat>, 'lat', 'detected lat column';
is @pairs[0]<lon>, 'lon', 'detected lon column';

# Test lat/lon with prefix
my @prefix-cols = <name start_lat start_lon>;
my @prefix-pairs = $plugout.detect-latlon-pairs(@prefix-cols);
is @prefix-pairs.elems, 1, 'detected prefixed lat/lon pair';
is @prefix-pairs[0]<lat>, 'start_lat', 'detected start_lat';
is @prefix-pairs[0]<lon>, 'start_lon', 'detected start_lon';

# Test HTML generation
my $temp-dir = $*TMPDIR.child('csvgeo-test-' ~ $*PID);
$temp-dir.mkdir;

my $csv-file = $temp-dir.child('test.csv');
spurt $csv-file, "point,name\n" ~
  '"{\"type\":\"Point\",\"coordinates\":[4.37,50.81]}",Brussels' ~ "\n";

$plugout.execute(path => $csv-file, data-dir => $temp-dir, name => 'test');
my $html-file = $temp-dir.child('test-csv-geo.html');
ok $html-file.e, 'HTML file created';

my $html = slurp $html-file;
ok $html ~~ /'Papa.parse'/, 'HTML contains Papa Parse';
ok $html ~~ /'Leaflet'/, 'HTML contains Leaflet';
ok $html ~~ /'wkx'/, 'HTML contains wkx library';
ok $html ~~ /'tryParseGeo'/, 'HTML contains geo parsing function';

done-testing;
