use Test;
use Samaki::Utils;
use Samaki::Plugin::Markdown;

# Mock shell-open to avoid opening browser
&shell-open.wrap: -> $path { }

my $plugin = Samaki::Plugin::Markdown.new;

my $cell = Samaki::Cell.new:
  page-name => 'test',
  content => '# Hello World',
  cell-type => 'markdown',
  name => 'test-md',
  data-dir => $*TMPDIR,
  wkdir => $*TMPDIR,
  :$plugin;

my $page = Samaki::Page.new:
  wkdir => $*TMPDIR,
  name => 'test',
  cells => [$cell];

$plugin.execute(:$cell, :$page, :mode<content>);

my $output = $cell.output-file.slurp;
ok $output.contains('Hello World'), 'markdown was converted';
ok $output.contains('<'), 'output contains HTML tags';

$cell.output-file.unlink;

# Test documentation example
my $cell2 = Samaki::Cell.new:
  page-name => 'test',
  content => q:to/MD/,
    # Hello, World!

    This is **Markdown** content with:

    * Lists
    * Links
    * _Emphasis_
    MD
  cell-type => 'markdown',
  name => 'doc',
  data-dir => $*TMPDIR,
  wkdir => $*TMPDIR,
  :$plugin;

my $page2 = Samaki::Page.new:
  wkdir => $*TMPDIR,
  name => 'test',
  cells => [$cell2];

$plugin.execute(:cell($cell2), :page($page2), :mode<content>);

my $output2 = $cell2.output-file.slurp;
ok $output2.contains('Hello, World!'), 'markdown contains heading text';
ok $output2.contains('<strong>Markdown</strong>'), 'bold text converted to strong tag';
ok $output2.contains('<em>Emphasis</em>'), 'italic text converted to em tag';
ok $output2.contains('<ul>') || $output2.contains('<li>'), 'list converted to HTML';

$cell2.output-file.unlink;

done-testing;
