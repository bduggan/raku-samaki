use Test;
use Samaki::Plugin::Duck;
use Samaki::Conf;

my $plugin = Samaki::Plugin::Duck.new;

# Check if duckdb is available
my $duck-available = try {
  my $version = qqx[duckdb --version 2>/dev/null];
  $version.chars > 0;
}

if $duck-available {
  my $conf-file = $*CWD.child('resources').child('samaki-conf-default.raku');
  my $conf = Samaki::Conf.new(:file($conf-file));
  lives-ok { $plugin.setup(:$conf) }, 'setup succeeds when duckdb is available';
  ok $plugin.version-info, 'version info is set';
  ok $plugin.version-info.contains('v'), 'version info contains version number';
} else {
  skip 'duckdb not available', 3;
}

# Test documentation example
if $duck-available {
  my $cell = Samaki::Cell.new:
    page-name => 'test',
    content => "select 'hello' as greeting, 'world' as noun;",
    cell-type => 'duck',
    name => 'test-duck',
    data-dir => $*TMPDIR,
    wkdir => $*TMPDIR,
    :$plugin;

  my $page = Samaki::Page.new:
    wkdir => $*TMPDIR,
    name => 'test',
    cells => [$cell];

  my $out-file = $cell.output-file;
  $out-file.parent.mkdir unless $out-file.parent.e;
  my $out = $out-file.open(:w);
  $plugin.execute(:$cell, :$page, :mode<content>, :$out);

  ok $out-file.e, 'output file was created';
  my $content = $out-file.slurp;
  ok $content.contains('greeting,noun'), 'output contains CSV headers';
  ok $content.contains('hello,world'), 'output contains data';

  ok $plugin.output, 'plugin output is set';
  ok $plugin.output.^name eq 'Array', 'plugin output is an array';
  ok $plugin.output.elems > 0, 'plugin output has elements';

  $out-file.unlink;
} else {
  skip 'duckdb not available', 6;
}

done-testing;
